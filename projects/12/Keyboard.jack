// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var int v;
        let v = Memory.peek(24576);
        if (~(v = 0)) {
            return v;
        }
        return 0;
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c, temp;
        let c = 0;
        let temp = -1;

        // Print cursor
        do Output.printChar(0);

        // Wait for user to press key
        while (c = 0) {
            let c = Keyboard.keyPressed();
        }

        // Wait for user to let go
        while (~(temp = 0)) {
            let temp = Keyboard.keyPressed();
        }

        // Print key
        // Ignore cursor clear on backspace
        if (~(c = String.backSpace())) {
            do Output.backSpace();
        }
        do Output.printChar(c);
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s;
        var char c;
        let s = String.new(64);     // Assume string does not go beyond one line

        do Output.printString(message);

        while(true) {
            let c = Keyboard.readChar();
            if (c = String.newLine()) {
                // Enter, print newline and return string
                return s;
            } else {
                if (c = String.backSpace()) {
                    // Backspace, erase last char
                    if (s.length() > 0) {
                        do Output.backSpace();
                        do s.eraseLastChar();
                    }
                } else {
                    // Otherwise, append char
                    let s = s.appendChar(c);
                }
            }
        }
        return s;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;
        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}
